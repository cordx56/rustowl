# Maintainer: MuntasirSZN <muntasir.joypurhat@gmail.com>
# Maintainer: cordx56 <cordx56@cordx.cx>

pkgname=rustowl-git
pkgver=r1.0
pkgrel=1
pkgdesc='Visualize Ownership and Lifetimes in Rust'
url='https://github.com/cordx56/rustowl'
license=('MPL-2.0')
makedepends=('git' 'rustup' 'gcc' 'make' 'autoconf' 'jemalloc')
arch=('any')
source=("git+https://github.com/cordx56/rustowl.git")
sha256sums=('SKIP')
conflicts=('rustowl-bin' 'rustowl')

pkgver() {
    cd "$srcdir/rustowl"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/rustowl"
    export "$(/bin/sh "./scripts/build/print-env.sh" "$(cat "./scripts/build/channel")")"
    ./scripts/build/toolchain cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$srcdir/rustowl"
    export RUSTOWL_RUNTIME_DIRS=/opt/rustowl
    export RUSTFLAGS="-C link-arg=-ljemalloc"
    ./scripts/build/toolchain cargo build --frozen --release --all-features --target "$(rustc --print=host-tuple)"
}

check() {
    cd "$srcdir/rustowl"
    export "$(/bin/sh "./scripts/build/print-env.sh" "$(cat "./scripts/build/channel")")"
    ./scripts/build/toolchain cargo test --frozen --all-features
}

package() {
    cd "$srcdir/rustowl"
    find target -type d | grep -E 'rustowl-build-time-out$' | xargs -I % cp -r % ./
    ./target/"$(rustc --print=host-tuple)"/release/rustowl toolchain install --path sysroot/"$(rustup show active-toolchain | awk '{ print $1 }')" --skip-rustowl-toolchain
    install -d -m 755 "$pkgdir/opt/rustowl"
    cp -a sysroot/ "$pkgdir/opt/rustowl/"
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/$(rustc --print=host-tuple)/release/rustowl"
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/$(rustc --print=host-tuple)/release/rustowlc"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/rustowl/LICENSE"
    install -Dm644 rustowl-build-time-out/man/rustowl.1 "$pkgdir/usr/share/man/man1/rustowl.1"
    install -Dm644 "rustowl-build-time-out/completions/rustowl.bash" "${pkgdir}/usr/share/bash-completion/completions/rustowl"
    install -Dm644 "rustowl-build-time-out/completions/_rustowl" "${pkgdir}/usr/share/zsh/site-functions/_rustowl"
    install -Dm644 "rustowl-build-time-out/completions/rustowl.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/rustowl.fish"
    install -Dm644 "rustowl-build-time-out/completions/rustowl.elv" "${pkgdir}/usr/share/elvish/completions/rustowl.elv"
    install -Dm644 "rustowl-build-time-out/completions/_rustowl.ps1" "${pkgdir}/usr/share/powershell/Modules/Rustowl/_rustowl.ps1"
}
